import "../../justfile"

# A quick test to see if the sim can pick up your RC.
rc:
  #!/usr/bin/env bash
  cargo run -p cu-basic-fc --no-default-features --features sim --bin rc-tester

logreader log="logs/embedded.copper":
  #!/usr/bin/env bash
  set -euo pipefail
  invocation_dir="{{invocation_directory()}}"
  log="{{log}}"
  if [[ "$log" != /* ]]; then
    log="${invocation_dir}/${log}"
  fi
  RUST_BACKTRACE=1 cargo run -p cu-basic-fc --no-default-features --features logreader --bin quad-logreader -- "$log" extract-copperlists --export-format json

fsck log="logs/embedded.copper":
  #!/usr/bin/env bash
  set -euo pipefail
  invocation_dir="{{invocation_directory()}}"
  log="{{log}}"
  if [[ "$log" != /* ]]; then
    log="${invocation_dir}/${log}"
  fi
  RUST_BACKTRACE=1 cargo run -p cu-basic-fc --no-default-features --features logreader --bin quad-logreader -- "$log" fsck

textlogs log="logs/embedded.copper" index="../../target/debug/cu29_log_index":
  #!/usr/bin/env bash
  set -euo pipefail
  invocation_dir="{{invocation_directory()}}"
  log="{{log}}"
  index="{{index}}"
  if [[ "$log" != /* ]]; then
    log="${invocation_dir}/${log}"
  fi
  if [[ "$index" != /* ]]; then
    index="${invocation_dir}/${index}"
  fi
  [[ -e "$index" ]] || { echo "log index not found: $index" >&2; exit 1; }
  RUST_BACKTRACE=1 cargo run -p cu-basic-fc --no-default-features --features logreader --bin quad-logreader -- "$log" extract-text-log "$index"

fw:
  #!/usr/bin/env bash
  set -euo pipefail
  cargo run --release --config .cargo/config-firmware.toml --no-default-features --features firmware,textlogs -p cu-basic-fc --bin quad

# Release release. With no textlogs.
fwr:
  #!/usr/bin/env bash
  set -euo pipefail
  cargo run --release --config .cargo/config-firmware.toml --no-default-features --features firmware -p cu-basic-fc --bin quad

# Debug (it won't be fast enough to run be at least you van debug the bringup).
fwd:
  #!/usr/bin/env bash
  set -euo pipefail
  cargo run --config .cargo/config-firmware.toml --no-default-features --features firmware,log-level-debug -p cu-basic-fc --bin quad

# Attach to the already-flashed target without rebuilding or flashing.
attach:
  #!/usr/bin/env bash
  set -euo pipefail
  probe-rs attach --chip STM32H743VITx --protocol swd ../../target/thumbv7em-none-eabihf/release/quad
